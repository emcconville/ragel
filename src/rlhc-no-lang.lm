void nolang_tok_list( TL: host::tok* )
{
	for Tok: host::tok in repeat(TL) {
		if match Tok [host::`${ StmtList: stmt* host::`}$]
			nolang_stmt_list( StmtList )
		elsif match Tok [host::`={ Expr: expr host::`}=]
			nolang_expr( Expr )
		else {
			send Output [Tok]
		}
	}
}

void nolang_expr_factor( ExprFactor: expr_factor )
{
	if match ExprFactor
		['$' string uint '{' TL: host::tok* host::`}$]
	{
		nolang_tok_list( TL )
	}
	elsif match ExprFactor
		['=' string uint '{' TL: host::tok* host::`}$]
	{
		nolang_tok_list( TL )
	}
	elsif match ExprFactor
		[O:`( TL: expr C: `)]
	{
		send Output
			[O nolang_expr(TL) C]
	}
	elsif match ExprFactor
		[ident O: `[ TL: expr C: `]]
	{
		send Output
			[ExprFactor.ident O nolang_expr( TL ) C]
	}
	elsif match ExprFactor
		['offset' '(' ident ',' expr ')']
	{
		send Output
			[ExprFactor.ident ' + ' nolang_expr( ExprFactor.expr )]
	}
	elsif match ExprFactor
		['deref' '(' ident ',' expr ')']
	{
		send Output
			"(*( [nolang_expr(ExprFactor.expr)] ))
	}
	elsif match ExprFactor
			[T: `TRUE]
	{
		T.data = '1'
		send Output [T]
	}
	elsif match ExprFactor
			[F: `FALSE]
	{
		F.data = '0'
		send Output [F]
	}
	elsif match ExprFactor
			[N: `nil]
	{
		N.data = '0'
		send Output [N]
	}
	elsif match ExprFactor
			[Number: number]
	{
		nolang_number( Number )
	}
	else {
		# Catches cases not specified
		send Output [ExprFactor]
	}
}

void nolang_expr_ptr( ExprPtr: expr_ptr )
{
	if match ExprPtr [expr_ptr Arrow: `-> expr_factor]
	{
		nolang_expr_ptr( ExprPtr._expr_ptr )
		send Output [Arrow]
		nolang_expr_factor( ExprPtr.expr_factor )
	}
	elsif match ExprPtr [Star: `* expr_ptr]
	{
		send Output [Star]
		nolang_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [PP: `++ expr_ptr]
	{
		send Output [PP]
		nolang_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [MM: `-- expr_ptr]
	{
		send Output [MM]
		nolang_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [expr_ptr PP: `++]
	{
		nolang_expr_ptr( ExprPtr._expr_ptr )
		send Output [PP]
	}
	elsif match ExprPtr [expr_ptr MM: `--]
	{
		nolang_expr_ptr( ExprPtr._expr_ptr )
		send Output [MM]
	}
	elsif match ExprPtr [Open: `( Type: type Close: `) expr_ptr]
	{
		send Output [Open]
		nolang_type( Type )
		send Output [Close]
		nolang_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [expr_factor]
	{
		nolang_expr_factor( ExprPtr.expr_factor )
	}
	else {
		send Output [ExprPtr]
	}
}

void nolang_expr_factor_op( ExprFactorOp: expr_factor_op )
{
	if match ExprFactorOp [B: `! expr_factor_op]
	{
		send Output [B]
		nolang_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [T: `~ expr_factor_op]
	{
		send Output [T]
		nolang_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [expr_ptr]
	{
		nolang_expr_ptr( ExprFactorOp.expr_ptr )
	}
}

void nolang_expr_bitwise( ExprBitwise: expr_bitwise )
{
	if match ExprBitwise [expr_bitwise A: `& expr_factor_op]
	{
		nolang_expr_bitwise( ExprBitwise._expr_bitwise )
		send Output [A]
		nolang_expr_factor_op( ExprBitwise.expr_factor_op )
	}
	elsif match ExprBitwise [expr_factor_op]
	{
		nolang_expr_factor_op( ExprBitwise.expr_factor_op )
	}
}

void nolang_expr_mult( ExprMult: expr_mult )
{
	if match ExprMult [expr_mult T: `* expr_bitwise]
	{
		nolang_expr_mult( ExprMult._expr_mult )
		send Output [T]
		nolang_expr_bitwise( ExprMult.expr_bitwise )
	}
	elsif match ExprMult [expr_bitwise]
	{
		nolang_expr_bitwise( ExprMult.expr_bitwise )
	}
}

void nolang_expr_add( ExprAdd: expr_add )
{
	if match ExprAdd [expr_add Op: add_op expr_mult]
	{
		nolang_expr_add( ExprAdd._expr_add )
		send Output [Op]
		nolang_expr_mult( ExprAdd.expr_mult )
	}
	elsif match ExprAdd [expr_mult]
	{
		nolang_expr_mult( ExprAdd.expr_mult )
	}
}

void nolang_expr_shift( ExprShift: expr_shift )
{
	if match ExprShift [expr_shift Op: shift_op expr_add]
	{
		nolang_expr_shift( ExprShift._expr_shift )
		send Output [Op]
		nolang_expr_add( ExprShift.expr_add )
	}
	elsif match ExprShift [expr_add]
	{
		nolang_expr_add( ExprShift.expr_add )
	}
}

void nolang_expr_test( ExprTest: expr_test )
{
	if match ExprTest [expr_test Op: test_op expr_shift]
	{
		nolang_expr_test( ExprTest._expr_test )
		send Output [Op]
		nolang_expr_shift( ExprTest.expr_shift )
	}
	elsif match ExprTest [expr_shift]
	{
		nolang_expr_shift( ExprTest.expr_shift )
	}
}

void nolang_expr_cond( ExprCond: expr_cond )
{
	nolang_expr_test( ExprCond.expr_test )
}

void nolang_expr_assign( ExprAssign: expr_assign )
{
	if match ExprAssign [expr_assign Op: assign_op expr_cond]
	{
		nolang_expr_assign( ExprAssign._expr_assign )
		send Output [Op]
		nolang_expr_cond( ExprAssign.expr_cond )
	}
	elsif match ExprAssign [expr_cond]
	{
		nolang_expr_cond( ExprAssign.expr_cond )
	}
}

void nolang_expr( Expr: expr )
{
	nolang_expr_assign( Expr.expr_assign )
}

void nolang_type( Type: type )
{
	if match Type "s8"
		send Output ['char ']
	elsif match Type "s16"
		send Output ['short ']
	elsif match Type "s32"
		send Output ['int ']
	elsif match Type "s64"
		send Output ['long ']
	elsif match Type "s128"
		send Output ['long long ']
	elsif match Type "uint"
		send Output ['unsigned int ']
	else
		send Output [Type]
}

void nolang_number( Number: number )
{
	if match Number [`u `( uint `) ]
		send Output "[Number.uint]u"
	else
		send Output [Number]
}

void nolang_num_list( NumList: num_list )
{
	for Number: number in NumList
		send Output "[nolang_number( Number )], "
}

void nolang_stmt( Stmt: stmt )
{
	if match Stmt
		['$' string uint '{' TL: host::tok* host::`}$]
	{
		nolang_tok_list( TL )
	}
	elsif match Stmt
		['=' string uint '{' TL: host::tok* host::`}$]
	{
		nolang_tok_list( TL )
	}
	elsif match Stmt [Array: array] {
		send Output
			"static const [nolang_type(Array.type)] "
				"[Array.ident] \[\] = { [nolang_num_list(Array.num_list)] };
	}
	elsif match Stmt [Value: value] {
		send Output
			"static const [Value.value_pass]
	}
	elsif match Stmt [
			'if' O: `( IfExpr: expr C: `) IfStmt: stmt
			ElseIfClauseList: else_if_clause* ElseClauseOpt: else_clause?
	] {
		send Output
			"if ( [nolang_expr(IfExpr)] )
			"	[nolang_stmt(IfStmt)]

		for ElseIfClause: else_if_clause in repeat( ElseIfClauseList ) {
			match ElseIfClause
				['else if (' ElseIfExpr: expr ')' ElseIfStmt: stmt]

			send Output
				"else if ( [nolang_expr(ElseIfExpr)] )
				"	[nolang_stmt(ElseIfStmt)]
		}

		if ( match ElseClauseOpt ['else' ElseStmt: stmt] ) {
			send Output
				"else
				"	[nolang_stmt(ElseStmt)]
		}
	}
	elsif match Stmt ['while' '(' WhileExpr: expr ')' WhileStmt: stmt] {
		send Output
			"while ( [nolang_expr(WhileExpr)] )
			"	[nolang_stmt(WhileStmt)]
	}
	elsif match Stmt ['switch' '(' SwitchExpr: expr ')' '{' StmtList: stmt* '}'] {
		send Output
			"switch ( [nolang_expr(SwitchExpr)] ) {
			"	[nolang_stmt_list(StmtList)]
			"}
	}
	elsif match Stmt [ExprExpr: expr Semi: `;] {
		send Output
			[nolang_expr(ExprExpr) Semi]
	}
	elsif match Stmt [L: `{ TL: stmt* R: `}] {
		send Output
			[L nolang_stmt_list(TL) R]
	}
	elsif match Stmt [TypeList: opt_const Type: type OptPtr: opt_ptr Ident: ident OptInit: opt_init Semi: `;]
	{
		send Output
			[TypeList nolang_type(Type) OptPtr Ident]

		if match OptInit [E: `= expr] {
			send Output
				[E nolang_expr(OptInit.expr)]
		}

		send Output
			[Semi]
	}
	elsif match Stmt [Export: export_stmt]
	{
		send Output
			"#define [Export.ident] [nolang_number(Export.number)] 
	}
	elsif match Stmt ['fallthrough' ';']
	{
		# Nothing needed here.
	}
	elsif match Stmt [Index: index_stmt]
	{
		send Output
			"const [nolang_type(Index.type)] *[Index.ident];
	}
	elsif match Stmt [case_label]
	{
		send Output
			"case [nolang_expr( Stmt.case_label.expr )]:
	}
	else {
		# catches unspecified cases
		send Output [Stmt]
	}
}

void nolang_stmt_list( StmtList: stmt* )
{
	for Stmt: stmt in repeat( StmtList )
		nolang_stmt( Stmt )
}